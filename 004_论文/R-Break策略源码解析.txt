//------------------------------------------------------------------------
// 基于TBL的R_Breaker 算法 
Params
Numeric notbef(9.00);//开盘时间
Numeric notaft(14.55);//收盘时间
Numeric f1(0.35);
Numeric f2(0.07);
Numeric f3(0.25);
Numeric reverse(1.00);
Numeric rangemin(0.2);
Numeric xdiv(3);//参数，用来调节Senter与Ssetup,或者Benter与Bsetup的距离，此参数越大，则距离越大

Vars
NumericSeries ssetup(0);
NumericSeries bsetup(0);
NumericSeries senter(0);
NumericSeries benter(0);
NumericSeries bbreak(0);
NumericSeries sbreak(0);
NumericSeries ltoday(0);
NumericSeries hitoday(9999);
NumericSeries startnow(0);
NumericSeries div(0);
BoolSeries rfilter(false);
Numeric i_reverse;
Numeric i_rangemin;
Numeric i_vB;
Numeric i_vS;

Begin
i_reverse = reverse*(OpenD(0)/100);			//查询当前bar的开盘价*reverse的值
i_rangemin = rangemin*(OpenD(0)/100);		//当前bar的开盘价*rangemin的值，该值为上下范围
if(BarStatus==0)							//如果是第一根bar
{
        startnow=0;							//设置startnow=0
        div=max(xdiv,1);					//设置div为xdiv与1的最大值
}

if(Date != Date[1])							//如果是每天的第一根bar
{
        SetGlobalVar(0,0);					//设置全局变量0为0
        SetGlobalVar(1,0);					//设置全局变量1为0
        startnow=startnow+1;				//startnow计数器加1
		
        ssetup=hitoday[1]+f1*(Close[1]-ltoday[1]);//计算ssetup卖出观察价为hitoday[1]昨日最高+f1(0.35)*（昨天收盘-昨天最低）
        senter=((1+f2)/2)*(hitoday[1]+Close[1])-(f2)*ltoday[1];//计算senter反转卖出价为((1+0.07)/2*(昨最高+昨收盘）-0.07*昨天最低
        benter=((1+f2)/2)*(ltoday[1]+Close[1])-(f2)*hitoday[1];//计算benter反转买入价为((1+0.07)/2*(昨最低+昨收盘）-0.07*昨天最高
        bsetup=ltoday[1]-f1*(hitoday[1]-Close[1]);//计算bsetup买入观察价为ltoday[1]昨日最低-f1(0.35)*（昨天最高-昨天收盘）
        bbreak=ssetup+f3*(ssetup-bsetup);//计算Bbreak突破买入价为ssetup卖出观察价+f3(0.25)*(卖出观察价-买入观察价)
        sbreak=bsetup-f3*(ssetup-bsetup);//计算Bbreak突破买入价为bsetup买入观察价+f3(0.25)*(卖出观察价-买入观察价)

        hitoday=High;	//更新最高数据
        ltoday=Low;		//更新最低数据

        rfilter=(hitoday[1]-ltoday[1])>=i_rangemin;//计算一个标志位，是否昨日的波动差大于当前开盘价的20%
}


if(High>hitoday)
{
        hitoday=High;	//更新最高数据
}
if(Low<ltoday)
{
        ltoday=Low;		//更新最低数据
}
if(Time*100>=notbef and Time*100<notaft and startnow>=2 and rfilter)//如果是要求的开盘时间且昨日波动幅度大于商品价格的20%
{

        if(Time != GetGlobalVar(1) and GetGlobalVar(1) != 0)
        {
                SetGlobalVar(1,10000);//为全局变量1设置一个初始值为10000，此处应存放时间
        }
        if(hitoday>=ssetup and marketposition>-1 and GetGlobalVar(1)<1)//如果当前最高价大于卖出观察价并且当前仓位为平仓或者多仓，且交易时间正常
        {
                If(Low<=(senter+(hitoday-ssetup)/div))//如果当前最低值小于（反转卖出价+（今日最高价-卖出观察价）/div除数）
                {
                        SellShort(1,senter+(hitoday-ssetup)/div);//则空头建仓，单位为1手，价格为senter+(hitoday-ssetup)/div
                        SetGlobalVar(1,Time);//设置全局变量1为交易时间
                        Return;
						//此策略为反转策略一，突破新高后反转。持多单时，当日内最高价超过观察卖出价后，盘中价格出现回落，且进一步跌破反转卖出价构成的支撑线时，采取反转策略，即在该点位做空。
                }
        }
        if(ltoday<=bsetup and marketposition<1  and GetGlobalVar(1)<1)//如果当前最低价小于买入观察价并且当前仓位为平仓或者空仓，且交易时间正常
        {
                If(High>=(benter-(bsetup-ltoday)/div))//如果当前最高值大于（反转买入价-（买入观察价-今日最低价）/div除数）
                {
                        Buy(1,benter-(bsetup-ltoday)/div);//则多头建仓，单位为1手，价格为benter-(bsetup-ltoday)/div
                        SetGlobalVar(1,Time);//设置全局变量1为交易时间
                        Return;
						//此策略为反转策略二，突破新低后反转。持空单，当日内最低价低于观察买入价后，盘中价格出现反弹，且进一步超过反转买入价构成的阻力线时，采取反转策略，即在该点位做多；
                }
        }

        if(marketposition==-1)//如果当前系统持有空单
        {
                SetGlobalVar(0,1);//设置全局变量为1，此变量为开关，控制当日是否继续开仓，1为当日不再因动量策略开仓
                if(High-EntryPrice>=i_reverse)//如果当前最高价格-建仓价格大于i_reverse，则止损离场并今日停止交易
                {
                        BuyToCover(1,entryprice+i_reverse);//空头平仓操作
                        Return;
                }
        }
        if(marketposition==1)//如果当前系统持有多单
        {
                SetGlobalVar(0,1);//设置全局变量为1，此变量为开关，控制当日是否继续开仓，1为当日不再因动量策略开仓
                if(EntryPrice-Low>=i_reverse)//如果建仓价格-当日最低价格大于i_reverse，则止损离场并今日停止交易
                {
                        Sell(1,entryprice-i_reverse);//多头平仓操作
                        Return;
                }
        }

        if(marketposition==0)//如果是平仓
        {
                if(High>=bbreak and GetGlobalVar(0) == 0)//如果今日最高突破了突破买入价，并且动量策略开仓开关为开启，则买入做多
                {
                        Buy(1,bbreak);//按照bbreak价格买入1手
                        Return;
						//突破策略一，确立涨势后追涨；
                }
        }
        if(marketposition==0)//如果是平仓
        {
                if(low<=sbreak  and GetGlobalVar(0) == 0)//如果今日最低突破了突破卖出价，并且动量策略开仓开关为开启，则卖出做空
                {
                        SellShort(1,sbreak);//按照sbreak价格卖出1手
                        Return;
						//突破策略二，确立跌势后追跌；
                }
        }

}



if(Time*100>=notaft and Time<0.1600)//如果时间大于指定的交易时间，则统一平仓
{

        if(marketposition==-1)
        {
                BuyToCover(1,Open);//如果是空头，则买入平仓,卖出价格为当前价格
        }
        if(marketposition==1)
        {
                Sell(1,Open);//如果是多头，则卖出平仓,卖出价格为当前价格
        }

}
End



//＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝＝